#title:{Conditions & Restarts}tag:"conditions"

Atomo uses a condition system very similar to Common Lisp's.

#section:{Signals}

#define:{
  signal: v
  > @ok
}body:{
  Sends a value to all bound handlers, nearest-first.

  #example:{
    { signal: @foo; signal: @bar } bind: { @foo -> "fooing" print }
  }
}

#define:{
  error: v
  > any
}body:{
  Signals #atomo:v as an error. If the #atomo:(signal:) call exits normally, a debugger is started. Hence, this method never exits normally.

  #example:{
    { "x" print; error: 1; "y" print } call
  }
}

#define:{
  warning: v
  > @ok
}body:{
  Similar to #atomo:(error:), but instead of starting a debugger, prints the error to #atomo:*error-output*.

  #example:{
    { "x" print; warning: 2; "y" print } call
  }
}

#define:{
  restart: name
  | name is-a?: Expression
  > any
}body:{
  Finds restart #atomo:name and jumps to it.

  #example:{
    action := { signal: @foo; "hi" print } with-restarts: { skip -> @skipped }
    { action } bind: { @foo -> restart: 'skip }
  }
}

#section:{Handlers}

#define:{
  action bind: handlers
  | action is-a?: Block
  | handlers is-a?: Block
  > any
}body:{
  Performs #atomo:action, with a handler for all signals matched in #atomo:handlers block. Returns the result of #atomo:action.

  The contents of #atomo:handlers should be #atomo:Association expressions, e.g. #atomo:(pattern -> responder), where #atomo:pattern is a match on the signal value, and #atomo:responder is either a #atomo:Block (which will be called with the signal value itself as an argument) or an expression to be evaluated in response to the signal.

  #example:{
    { signal: @foo } bind: { @foo -> "fooed!" print }
    { signal: @(bar: 42) } bind: { @(bar: x) -> @(fooed: x) print }
    { error: @uh-oh } bind: { Error -> { e | @(got-error: e) print } }
  }

  A #atomo:responder may often invoke #atomo:(restart:) or #atomo:(restart:with:), which is the only way they can affect the return value of #atomo:action.

  After a handler responds to a signal, the signal continues outward to the next recently bound handler:

  #example:{
    { { signal: @foo } bind: { @foo -> "me first!" print } } bind: { @foo -> "no me!" print }
  }
}

#define:{
  action with-restarts: restarts
  | action is-a?: Block
  | restarts is-a?: Block
  > any
}body:{
  Call #atomo:action, binding the restarts described by #atomo:restarts. These restarts, when invoked, will replace the result of #atomo:action.

  The contents of #atomo:restarts should be #atomo:Association expressions, e.g. #atomo:(name -> expression), where #atomo:name is the name for the restart, and #atomo:expression is the action to be performed when the restart is invoked.

  #example:{
    { { x } with-restarts: { ignore -> @ok } } bind: { Error -> restart: 'ignore }
  }
}

#define:{
  restart: name with: arguments
  | name is-a?: Expression
  | arguments is-a?: List
  > any
}body:{
  Invokes restart #atomo:name with #atomo:arguments passed to the block.

  #example:{
    action := { signal: @foo; "hi" print } with-restarts: { use-value -> { v | v } }
    { action } bind: { @foo -> restart: 'use-value with: [42] }
  }
}

#define:{
  find-restart: name
  | name is-a?: Expression
  > in?: [@(ok: Restart), @none]
}body:{
  Looks for a restart named #atomo:name.

  #example:{
    action := { signal: @foo; "hi" print } with-restarts: { skip -> @skipped }
    { action } bind: { @foo -> [find-restart: 'skip, find-restart: 'bar] print }
  }
}

#section:{Traditional Exceptions}

These methods are specific to errors, and provide a system similar to that of traditional exception handling. They are defined in terms of the more general condition system.

#define:{
  action catch: recover
  | action is-a?: Block
  | recover is-a?: Block
  > any
}body:{
  Execute #atomo:(action call), catching any errors and passing them to #atomo:recover for handling.

  Yields the result of #atomo:action or #atomo:recover.

  #example:{
    { error: @error } catch: { e | @(got: e) }
    { @ok } catch: { @caught }
  }
}

#define:{
  action catch: recover ensuring: cleanup
  | [action, recover, cleanup] all?: @(is-a?: Block)
  > any
}body:{
  Execute #atomo:(action call), catching any errors and passing them to #atomo:recover for handling, and ensuring that #atomo:cleanup is called after the action is executed.

  Yields the result of #atomo:action or #atomo:recover.

  #example:{
    { @ok } catch: { @caught } ensuring: { "cleaning up" print }
    { error: @not-ok } catch: { @caught } ensuring: { "cleaning up" print }
  }
}

#define:{
  action handle: branches
  | action is-a?: Block
  | branches is-a?: Block
  > any
}body:{
  Similar to #atomo:(catch:), but #atomo:branches is a block containing exception patterns bound to the action to take if they're thrown. If none of the exceptions match any of the patterns, the exception is re-thrown.

  #example:{
    { error: @foo } handle: { @foo -> 1; @bar -> 2 }
    { error: @bar } handle: { @foo -> 1; @bar -> 2 }
    { error: @baz } handle: { @foo -> 1; @bar -> 2 }
    { @ok } handle: { @foo -> 1; @bar -> 2 }
  }
}

#define:{
  action handle: branches ensuring: cleanup
  | [action, branches, cleanup] all?: @(is-a?: Block)
  > any
}body:{
  Similar to #atomo:(handle:), but with an #atomo:(ensuring:) block.

  #example:{
    { error: @foo } handle: { @foo -> 1 } ensuring: { "hi" print }
    { error: @baz } handle: { @foo -> 1 } ensuring: { "hi" print }
    { @ok } handle: { @foo -> 1 } ensuring: { "hi" print }
  }
}

#define:{
  action ensuring: cleanup
  | action is-a?: Block
  | cleanup is-a?: Block
  > any
}body:{
  Execute #atomo:(action call), ensuring that #atomo:cleanup is called after the action is executed.

  Yields the result of #atomo:action.

  #example:{
    { @ok } ensuring: { "cleaning up" print }
    { error: @not-ok } ensuring: { "cleaning up" print }
  }
}

#define:{
  value ensuring: cleanup do: action
  | cleanup is-a?: Block
  | action is-a?: Block
  > any
}body:{
  Similar to #atomo:(ensuring:), but #atomo:value is passed to both blocks. This makes things like file opening and closing very natural.

  Yields the result of #atomo:action.

  #example:{
    File (new: "example-file") ensuring: @close do: @(display: "Hello!")
  }

  #(@ok = File delete: "example-file")
}
